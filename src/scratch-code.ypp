%require "3.0.4"																									//require a specific bison version (from https://www.gnu.org/software/bison/manual/html_node/Calc_002b_002b-Parser.html)
%language "c++"
%defines																											//many of the following directive are explained here: https://www.gnu.org/software/bison/manual/html_node/Decl-Summary.html
%define parser_class_name {ScratchCodeParser}
%define api.token.constructor
%define api.value.type variant																						//use fancy variants to be compatible with C++ (from https://www.gnu.org/software/bison/manual/html_node/C_002b_002b-Variants.html)
%define parse.assert
%code requires
{
	#include <string>
	#include <algorithm>
	#include <memory>
	#include <typeinfo>
	#include <ast/AST.hpp>
	class ScratchCodeDriver;
}



//the parsing context
%param { ScratchCodeDriver& driver }																				//a reference to a "ScratchCodeDriver" will become a parameter for the "ScratchCodeParser" class constructor
%locations
%initial-action
{
	//initialize the initial location (@$ evaluates to "yyla.location", thus it is not directly associated with my code)
	//"yyla" is in gen/scratch-code.tab.cpp and is a "symbol_type"=instance of "basic_symbol<by_type>" templated class
	//"basic_symbol" class contains member "location" of type "location_type"=instance of "location" class
	//"location" class is in gen/location.hh, has "begin" and "end" members of type "position"
	//"position" class is in gen/position.hh, has "filename" member of type std::string*
	@$.begin.filename = @$.end.filename = driver.getFilenamePointer();
};
%define parse.trace
%define parse.error verbose

%code
{
	#include "ScratchCodeDriver.hpp"
}

%define api.token.prefix {TOK_}
%token
	END 0 "end of file"
	IF "if"
	ELSE "else"
	WHILE "while"
	FOR "for"
	RETURN "return"
	ROUND_BRACKET_OPEN "("
	ROUND_BRACKET_CLOSED ")"
	SQUARE_BRACKET_OPEN "["
	SQUARE_BRACKET_CLOSED "]"
	CURLY_BRACKET_OPEN "{"
	CURLY_BRACKET_CLOSED "}"
	COMMA ","
	SEMICOLON ";"

	OP_PLUS "+"
	OP_MINUS "-"
	OP_INCREMENT "++"
	OP_DECREMENT "--"
	OP_LOGICAL_NOT "!"
	OP_BITWISE_NOT "~"
	OP_TYPECAST_BOOL "(bool)"
	OP_TYPECAST_INT "(int)"
	OP_TYPECAST_REAL "(real)"
	OP_TYPECAST_STRING "(string)"
	
	OP_MULTIPLY "*"
	OP_DIVIDE "/"
	OP_MODULO "%"
	OP_BITWISE_AND "&"
	OP_BITWISE_OR "|"
	OP_BITWISE_XOR "^"
	OP_BITSHIFT_LEFT "<<"
	OP_BITSHIFT_RIGHT ">>"
	OP_LOGICAL_AND "&&"
	OP_LOGICAL_OR "||"
	OP_LESS_THAN "<"
	OP_LESS_THAN_OR_EQUAL "<="
	OP_GREATER_THAN ">"
	OP_GREATER_THAN_OR_EQUAL ">="
	OP_EQUAL "=="
	OP_NOT_EQUAL "!="

	OP_ASSIGNMENT "="
	OP_ADD_ASSIGNMENT "+="
	OP_SUBTRACT_ASSIGNMENT "-="
	OP_MULTIPLY_ASSIGNMENT "*="
	OP_DIVIDE_ASSIGNMENT "/="
	OP_MODULO_ASSIGNMENT "%="
	OP_BITWISE_AND_ASSIGNMENT "&="
	OP_BITWISE_OR_ASSIGNMENT "|="
	OP_BITWISE_XOR_ASSIGNMENT "^="
	OP_BITSHIFT_LEFT_ASSIGNMENT "<<="
	OP_BITSHIFT_RIGHT_ASSIGNMENT ">>=";
	
%token <std::string>									IDENTIFIER "identifier"										//define complex terminal symbol token types
%token <ast::Lexer::ParsedVariableType>					VARIABLE_TYPE "variable type"
%token <ast::Lexer::ParsedLoopControlStatement>			LOOP_CONTROL_STATEMENT "loop control statement"
%token <std::shared_ptr<ast::RValueValue>>				RVALUE_VALUE "rvalue value"
%type <std::shared_ptr<ast::Statement>>					statement;													//define non-terminal symbol token types
%type <std::shared_ptr<ast::StatementList>>				statements statementsBlockInternal statementsBlock;
%type <std::shared_ptr<ast::VariableDefinitionList>>	localVariableDefinitionList localVariableDefinitions;
%type <std::shared_ptr<ast::VariableDefinition>>		localVariableDefinition;
%type <std::shared_ptr<ast::Conditional>>				conditional conditionalIf;
%type <std::shared_ptr<ast::ForLoop>>					forLoop;
%type <std::shared_ptr<ast::WhileLoop>>					whileLoop;
%type <std::shared_ptr<ast::LoopControlStatement>>		loopControlStatement;
%type <std::shared_ptr<ast::FunctionDefinition>>		functionDefinition;
%type <std::shared_ptr<ast::VariableDefinitionList>>	functionDefinitionParameters functionDefinitionParametersMore;
%type <std::shared_ptr<ast::VariableDefinition>>		functionDefinitionParameter;
%type <std::shared_ptr<ast::ReturnStatement>>			returnStatement;
%type <std::shared_ptr<ast::Value>>						value valueStandalone;
%type <std::shared_ptr<ast::LValue>>					lValue;
%type <std::shared_ptr<ast::RValue>>					rValue;
%type <std::shared_ptr<ast::FunctionCall>>				functionCall;
%type <std::shared_ptr<ast::ValueList>>					functionCallParameters functionCallParametersMore;
%type <std::shared_ptr<ast::Operation>>					operation;
%printer { yyoutput << ast::Lexer::getString($$); } <ast::Lexer::ParsedVariableType>
%printer { yyoutput << typeid($$).name() << " at " << &$$; } <*>

%right "then" "else"																								//this way, shift will be preferred over reduce when parsing if-else-statements, from http://stackoverflow.com/a/12734499

%right "=" "+=" "-=" "*=" "/=" "%=" "&=" "|=" "^=" "<<=" ">>="														//for more detailed information on the used precedences, see LexerTokenDefinitions.hpp from scratch-code-ast
%left "||"
%left "&&"
%left "|"
%left "^"
%left "&"
%left "==" "!="
%left "<" "<=" ">" ">="
%left "<<" ">>"
%left "+" "-"																										//for dealing with the precedences of operators that look the same but have different meanings: http://dinosaur.compilertools.net/bison/bison_5.html#SEC27
%left "*" "/" "%"
%right "++" "--" UNOP_UNARY_PLUS UNOP_UNARY_MINUS "!" "~" "(bool)" "(int)" "(real)" "(string)"
%left UNOP_POSTFIX_INCREMENT UNOP_POSTFIX_DECREMENT



%%
%start unit;																										//begin with the "unit" rule as the highest-level-rule
unit:																												//this is the ONLY rule that writes to driver.syntaxTree
	statements END
	{
		*(driver.syntaxTree) = *($1);
		
		for(auto stmt : driver.syntaxTree->getStatements())
		{
			//there are only function definitions or variable definitions allowed at the very first level
			if(!stmt->isA<ast::FunctionDefinition>()  &&  !stmt->isA<ast::VariableDefinition>())
				throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations[stmt], "there may only be function or variable definitions at the uppermost level");
		
			//parents of statements on first level need to be assigned manually here (as "unit" expects "statements" and not "statementsBlock")
			stmt->setParent(driver.syntaxTree);
		}
		
		//recFuncs needs to be an lvalue for ast::doRecursively
		//here, basically just function call associations are made
		ast::RecursiveFunctionsType recFuncs =
		{
			{
				//add links to associated functions for all function calls
				ast::FunctionCall::uniqueId,
				[&](std::shared_ptr<ast::Node> obj)
				{
					auto funcCall = std::static_pointer_cast<ast::FunctionCall>(obj);
					
					//search for associated function in all defined functions ("funcCall->getAssocFunc()" is a dummy function here that just contains the function's name)
					std::vector<std::shared_ptr<ast::FunctionDefinition>>::iterator assocFunc = std::find_if(driver.functionDefinitions.begin(), driver.functionDefinitions.end(), [&](auto funcDef) { return (funcDef->getName() == funcCall->getAssocFunc()->getName()); });
					if(assocFunc == driver.functionDefinitions.end())
						throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations[funcCall], "'" + funcCall->getAssocFunc()->getName() + "' was not defined as a function in this scope");
					funcCall->setAssocFunc(*assocFunc);
		
					//check correct types for function call
					auto& assocFuncVarDefs = (*assocFunc)->getArgs()->getVarDefs();
					auto& funcCallArgsValues = funcCall->getArgs()->getValues();
					if(assocFuncVarDefs.size() != funcCallArgsValues.size())
						throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations[funcCall], "wrong number of arguments for function '" + (*assocFunc)->getName() + "' provided, expected " + std::to_string(assocFuncVarDefs.size()) + ", not " + std::to_string(funcCallArgsValues.size()));
					for(auto it = assocFuncVarDefs.begin(); it != assocFuncVarDefs.end(); ++it)
					{
						std::size_t i = std::distance(assocFuncVarDefs.begin(), it);
						ast::Lexer::ParsedVariableType valType = funcCallArgsValues[i]->getEffectiveType();
						if((*it)->getType() != valType)
							throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations[funcCall], "argument " + std::to_string(i+1) + " for function '" + (*assocFunc)->getName() + "' has wrong type, expected '" + ast::Lexer::getString((*it)->getType()) + "', not '" + ast::Lexer::getString(valType) + "'");
					}
				}
			},
			{
				//make sure that no function definition has another function definition as parent
				//also make sure that all function definitions are at the uppermost level (i.e. number of steps to at has to be 1)
				ast::FunctionDefinition::uniqueId,
				[&](std::shared_ptr<ast::Node> obj)
				{
					if(obj->hasParentWithId<ast::FunctionDefinition>())
						throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations.at(obj), "nested function definitions are not allowed");
					
					std::size_t steps;
					if(obj->hasParent<ast::StatementList>(driver.syntaxTree, &steps)  &&  steps > 1)
						throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations.at(obj), "all function definitions need to be at the uppermost level");
				}
			},
			{
				//set target loops for loop control statements
				ast::LoopControlStatement::uniqueId,
				[&](std::shared_ptr<ast::Node> obj)
				{
					auto lcs = std::static_pointer_cast<ast::LoopControlStatement>(obj);
					auto parentLoop = lcs->hasParentWithIdBase<ast::ControllableLoop>();
					if(parentLoop == nullptr)
						throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations.at(lcs), "loop control statement has no loop to target");
					else
					{
						lcs->setTargetLoop(parentLoop);
						driver.nodeLocations.erase(lcs);
					}
				}
			}
		};
		ast::doRecursively(driver.syntaxTree, recFuncs);
		
		//only after the function call associations happened, you can check the correctness of types, as the effective types of function calls were not known previously
		recFuncs =
		{
			{
				//check correct types for binary operation
				ast::BinaryOperation::uniqueId,
				[&](std::shared_ptr<ast::Node> obj)
				{
					auto binOp = std::static_pointer_cast<ast::BinaryOperation>(obj);
				
					//check if types on both sides are the same
					if(binOp->getLhs()->getEffectiveType() != binOp->getRhs()->getEffectiveType())
						throw yy::ScratchCodeParser::syntax_error(@$, "types for both sides of '" + ast::Lexer::getString(binOp->getOperation()) + "' need to be the same, are currently '" + ast::Lexer::getString(binOp->getLhs()->getEffectiveType()) + "' and '" + ast::Lexer::getString(binOp->getRhs()->getEffectiveType()) + "'");
					
					//check if allowed type for used operation is used (at this point, it's guaranteed that the types of lhs and rhs are the same)
					if(!ast::Lexer::isTypeAllowed(binOp->getOperation(), binOp->getLhs()->getEffectiveType()))
						throw yy::ScratchCodeParser::syntax_error(@$, "cannot apply '" + ast::Lexer::getString(binOp->getOperation()) + "' to two instances of '" + ast::Lexer::getString(binOp->getLhs()->getEffectiveType()) + "'");
				}
			},
			{
				//set target functions for return statements and check correct type
				ast::ReturnStatement::uniqueId,
				[&](std::shared_ptr<ast::Node> obj)
				{
					auto retStmt = std::static_pointer_cast<ast::ReturnStatement>(obj);
					auto parentFuncDef = obj->hasParentWithId<ast::FunctionDefinition>();
					if(parentFuncDef == nullptr)
						throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations.at(retStmt), "return statement has no function to target");
					else
					{
						ast::Lexer::ParsedVariableType retStmtValueType = (retStmt->getValue()==nullptr ? ast::Lexer::ParsedVariableType::Void : retStmt->getValue()->getEffectiveType());
						if(parentFuncDef->getReturnType() != retStmtValueType)
							throw yy::ScratchCodeParser::syntax_error(driver.nodeLocations.at(retStmt), "mismatching types for 'return', function '" + parentFuncDef->getName() + "' expects '" + ast::Lexer::getString(parentFuncDef->getReturnType()) + "', not '" + ast::Lexer::getString(retStmtValueType) + "'");
						retStmt->setTargetFunc(parentFuncDef);
						driver.nodeLocations.erase(retStmt);
					}
				}
			},
			{
				//check correct type for unary operations
				ast::UnaryOperation::uniqueId,
				[&](std::shared_ptr<ast::Node> obj)
				{
					auto unOp = std::static_pointer_cast<ast::UnaryOperation>(obj);
					if(!ast::Lexer::isTypeAllowed(unOp->getOperation(), unOp->getValue()->getEffectiveType()))
						throw yy::ScratchCodeParser::syntax_error(@$, "cannot apply '" + ast::Lexer::getString(unOp->getOperation()) + "' to instance of '" + ast::Lexer::getString(unOp->getValue()->getEffectiveType()) + "'");
				}
			}
		};
		ast::doRecursively(driver.syntaxTree, recFuncs);
	};



statement:																											//returns ast::Statement
	valueStandalone			{ $$ = $1; }
  | conditional				{ $$ = $1; }
  | forLoop					{ $$ = $1; }
  | whileLoop				{ $$ = $1; }
  | loopControlStatement	{ $$ = $1; }
  | functionDefinition		{ $$ = $1; }
  | returnStatement			{ $$ = $1; };
statements:																											//returns ast::StatementList
	%empty
	{
		$$ = std::make_shared<ast::StatementList>(nullptr);
	}
  | statements statement
	{
		$1->addStatement($2);
		$$ = $1;
	}
  | statements localVariableDefinitionList
	{
		for(auto varDef : $2->getVarDefs())
			$1->addStatement(varDef);
		$$ = $1;
	};
statementsBlockInternal:																							//returns ast::StatementList
	"{" statements "}"
	{
		$$ = $2;
	}
  | statement
	{
		auto newStmtList = std::make_shared<ast::StatementList>(nullptr);
		newStmtList->addStatement($1);
		$$ = newStmtList;
	};
statementsBlock:																									//returns ast::StatementList
	statementsBlockInternal
	{
		for(auto stmt : $1->getStatements())
			stmt->setParent($1);
		
		//clean up variable definitions that went out of scope
		auto i = driver.variableDefinitions.begin();
		while(i != driver.variableDefinitions.end())
		{
			if((*i)->hasParent<ast::StatementList>($1))
				i = driver.variableDefinitions.erase(i);
			else
				++i;
		}
		
		$$ = $1;
	};



localVariableDefinitionList:
	VARIABLE_TYPE localVariableDefinition localVariableDefinitions ";"												//returns ast::VariableDefinitionList
	{
		$3->getVarDefs().insert($3->getVarDefs().begin(), $2);
		for(auto varDef : $3->getVarDefs())
		{
			varDef->setType($1);
			driver.variableDefinitions.push_back(varDef);
		}
		$$ = $3;
	};
localVariableDefinitions:																							//returns ast::VariableDefinitionList
	%empty
	{
		$$ = std::make_shared<ast::VariableDefinitionList>(nullptr);
	}
  | localVariableDefinitions "," localVariableDefinition
	{
		$1->addVarDef($3);
		$$ = $1;
	};
localVariableDefinition:																							//returns ast::VariableDefinition
	IDENTIFIER
	{
		driver.throwIfDefined(@$, $1);
		auto newVarDef = std::make_shared<ast::VariableDefinition>(nullptr, ast::Lexer::ParsedVariableType::Invalid, $1);
		driver.nodeLocations[newVarDef] = @$;
		$$ = newVarDef;
	};



conditional:																										//returns ast::Conditional
	conditionalIf                 %prec "then"																		//"then" dummy token to solve dangling-else-/shift-reduce-problem, from http://stackoverflow.com/a/12734499
	{
		driver.nodeLocations[$1] = @$;
		$$ = $1;
	}
  | conditionalIf "else" statementsBlock
	{
		$3->setParent($1);
		$1->setAlternativeBody($3);
		driver.nodeLocations[$1] = @$;
		$$ = $1;
	};
conditionalIf:																										//returns ast::Conditional
	"if" "(" value ")" statementsBlock
	{
		auto newCond = std::make_shared<ast::Conditional>(nullptr);
		$3->setParent(newCond);
		$5->setParent(newCond);
		newCond->addCondition($3);
		newCond->addConsequenceBody($5);
		$$ = newCond;
	};
forLoop:																											//returns ast::ForLoop
	"for" "(" value ";" value ";" value ")" statementsBlock
	{
		auto newForLoop = std::make_shared<ast::ForLoop>(nullptr, $3, $5, $7, $9);
		$3->setParent(newForLoop);
		$5->setParent(newForLoop);
		$7->setParent(newForLoop);
		$9->setParent(newForLoop);
		driver.nodeLocations[newForLoop] = @$;
		$$ = newForLoop;
	};
whileLoop:																											//returns ast::WhileLoop
	"while" "(" value ")" statementsBlock
	{
		auto newWhileLoop = std::make_shared<ast::WhileLoop>(nullptr, $3, $5);
		$3->setParent(newWhileLoop);
		$5->setParent(newWhileLoop);
		driver.nodeLocations[newWhileLoop] = @$;
		$$ = newWhileLoop;
	};
loopControlStatement:																								//returns ast::LoopControlStatement
	LOOP_CONTROL_STATEMENT ";"
	{
		auto newLCS = std::make_shared<ast::LoopControlStatement>(nullptr, $1, nullptr);
		driver.nodeLocations[newLCS] = @$;
		$$ = newLCS;
	};



functionDefinition:																									//returns ast::FunctionDefinition
	VARIABLE_TYPE IDENTIFIER "(" functionDefinitionParameters ")" statementsBlock
	{
		driver.throwIfDefined(@$, $2);
		auto newFuncDef = std::make_shared<ast::FunctionDefinition>(nullptr, $1, $2, $4, $6);
		$4->setParent(newFuncDef);
		$6->setParent(newFuncDef);
		driver.functionDefinitions.push_back(newFuncDef);
		driver.nodeLocations[newFuncDef] = @$;
		
		//clean up variable definitions of function defintion parameters as the function definition has ended at this point
		for(auto vd : newFuncDef->getArgs()->getVarDefs())															//erase-remove stuff from http://stackoverflow.com/a/3385251
		{
			driver.variableDefinitions.erase(std::remove(driver.variableDefinitions.begin(), driver.variableDefinitions.end(), vd), driver.variableDefinitions.end());
			driver.nodeLocations.erase(vd);
		}
		
		$$ = newFuncDef;
	};
functionDefinitionParameters:																						//returns ast::VariableDefinitionList
	%empty
	{
		$$ = std::make_shared<ast::VariableDefinitionList>(nullptr);
	}
  | functionDefinitionParameter functionDefinitionParametersMore
	{
		$2->getVarDefs().insert($2->getVarDefs().begin(), $1);
		$$ = $2;
	};
functionDefinitionParametersMore:																					//returns ast::VariableDefinitionList
	%empty
	{
		$$ = std::make_shared<ast::VariableDefinitionList>(nullptr);
	}
  | functionDefinitionParametersMore "," functionDefinitionParameter
	{
		$1->addVarDef($3);
		$3->setParent($1);
		$$ = $1;
	};
functionDefinitionParameter:																						//returns ast::VariableDefinition
	VARIABLE_TYPE IDENTIFIER
	{
		driver.throwIfDefined(@$, $2);
		auto newVarDef = std::make_shared<ast::VariableDefinition>(nullptr, $1, $2);
		driver.variableDefinitions.push_back(newVarDef);
		driver.nodeLocations[newVarDef] = @$;
		$$ = newVarDef;
	};
returnStatement:																									//returns ast::ReturnStatement
	"return" ";"
	{
		auto newRetStmt = std::make_shared<ast::ReturnStatement>(nullptr);
		driver.nodeLocations[newRetStmt] = @$;
		$$ = newRetStmt;
	}
  | "return" value ";"
	{
		auto newRetStmt = std::make_shared<ast::ReturnStatement>(nullptr, $2, nullptr);
		$2->setParent(newRetStmt);
		driver.nodeLocations[newRetStmt] = @$;
		$$ = newRetStmt;
	};



valueStandalone:																									//returns ast::Value
	value ";"
	{
		driver.nodeLocations[$1] = @$;
		$$ = $1;
	};
value:																												//returns ast::Value
	lValue					{ $$ = $1; }
  | rValue					{ $$ = $1; }
  | functionCall			{ $$ = $1; }
  | "(" value ")"
	{
		$$ = $2;
	};
  | operation
	{
		if($1->isA<ast::UnaryOperation>())
		{
			auto unOp = std::static_pointer_cast<ast::UnaryOperation>($1);
			unOp->getValue()->setParent(unOp);
			
			//check correct value category
			auto valCat = unOp->getValue()->getValueCategory();
			auto reqValCat = unOp->getRequiredValueCategory();
			if(reqValCat != ast::Lexer::ValueCategory::Any  &&  reqValCat != valCat)
				throw yy::ScratchCodeParser::syntax_error(@$, "unexpected " + ast::Lexer::getString(valCat) + " for value of '" + ast::Lexer::getString(unOp->getOperation()) + "', expecting " + ast::Lexer::getString(reqValCat) + "");
			
			$$ = unOp;
		}
		else if($1->isA<ast::BinaryOperation>())
		{
			auto binOp = std::static_pointer_cast<ast::BinaryOperation>($1);
			binOp->getLhs()->setParent(binOp);
			binOp->getRhs()->setParent(binOp);
			
			//check correct value categories for both sides
			auto lhsValCat = binOp->getLhs()->getValueCategory(), rhsValCat = binOp->getRhs()->getValueCategory();
			auto lhsReqValCat = binOp->getRequiredLhsValueCategory(), rhsReqValCat = binOp->getRequiredRhsValueCategory();
			if(lhsReqValCat != ast::Lexer::ValueCategory::Any  &&  lhsReqValCat != lhsValCat)
				throw yy::ScratchCodeParser::syntax_error(@$, "unexpected " + ast::Lexer::getString(lhsValCat) + " for lhs of '" + ast::Lexer::getString(binOp->getOperation()) + "', expecting '" + ast::Lexer::getString(lhsReqValCat) + "'");
			if(rhsReqValCat != ast::Lexer::ValueCategory::Any  &&  rhsReqValCat != rhsValCat)
				throw yy::ScratchCodeParser::syntax_error(@$, "unexpected " + ast::Lexer::getString(rhsValCat) + " for rhs of '" + ast::Lexer::getString(binOp->getOperation()) + "', expecting '" + ast::Lexer::getString(rhsReqValCat) + "'");
			
			$$ = binOp;
		}
		else
		{
			throw yy::ScratchCodeParser::syntax_error(@$, "fatal inconsistency error");
		}
	};
lValue:																												//returns ast::LValue
	IDENTIFIER
	{
		std::vector<std::shared_ptr<ast::VariableDefinition>>::iterator assocVarDef = std::find_if(driver.variableDefinitions.begin(), driver.variableDefinitions.end(), [&](auto varDef) { return (varDef->getName() == $1); });
		if(assocVarDef == driver.variableDefinitions.end())
			throw yy::ScratchCodeParser::syntax_error(@$, "'" + $1 + "' was not defined as a variable in this scope");
		$$ = std::make_shared<ast::LValue>(nullptr, *assocVarDef);
	};
rValue:																												//returns ast::RValue
	RVALUE_VALUE			{ $$ = $1; };



functionCall:																										//returns ast::FunctionCall
	IDENTIFIER "(" functionCallParameters ")"
	{
		//associated function is searched for at the very end to not make function predeclarations necessary
		//also use a dummy function for later that just keeps the function's name
		auto newFuncCall = std::make_shared<ast::FunctionCall>(nullptr, std::make_shared<ast::FunctionDefinition>(nullptr, ast::Lexer::ParsedVariableType::Invalid, $1, nullptr, nullptr), $3);
		$3->setParent(newFuncCall);
		driver.nodeLocations[newFuncCall] = @$;
		$$ = newFuncCall;
	};
functionCallParameters:																								//returns ast::ValueList
	%empty
	{
		$$ = std::make_shared<ast::ValueList>(nullptr);
	}
  | value functionCallParametersMore
	{
		$2->getValues().insert($2->getValues().begin(), $1);
		$$ = $2;
	};
functionCallParametersMore:																							//returns ast::ValueList
	%empty
	{
		$$ = std::make_shared<ast::ValueList>(nullptr);
	}
  | functionCallParametersMore "," value
	{
		$1->addValue($3);
		$3->setParent($1);
		$$ = $1;
	};



operation:																											//returns ast::Operation (sadly one cannot create a rule like "binaryOperationSymbol" to group symbols, from http://stackoverflow.com/a/13567518)
	value "=" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Assignment, $3); }
  | value "+=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::AddAssignment, $3); }
  | value "-=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::SubtractAssignment, $3); }
  | value "*=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::MultiplyAssignment, $3); }
  | value "/=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::DivideAssignment, $3); }
  | value "%=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::ModuloAssignment, $3); }
  | value "&=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitwiseAndAssignment, $3); }
  | value "|=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitwiseOrAssignment, $3); }
  | value "^=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitwiseXorAssignment, $3); }
  | value "<<=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitshiftLeftAssignment, $3); }
  | value ">>=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitshiftRightAssignment, $3); }
  | value "||" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::LogicalOr, $3); }
  | value "&&" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::LogicalAnd, $3); }
  | value "|" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitwiseOr, $3); }
  | value "^" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitwiseXor, $3); }
  | value "&" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitwiseAnd, $3); }
  | value "==" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Equal, $3); }
  | value "!=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::NotEqual, $3); }
  | value "<" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::LessThan, $3); }
  | value "<=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::LessThanOrEqual, $3); }
  | value ">" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::GreaterThan, $3); }
  | value ">=" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::GreaterThanOrEqual, $3); }
  | value "<<" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitshiftLeft, $3); }
  | value ">>" value	{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::BitshiftRight, $3); }
  | value "+" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Add, $3); }
  | value "-" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Subtract, $3); }
  | value "*" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Multiply, $3); }
  | value "/" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Divide, $3); }
  | value "%" value		{ $$ = std::make_shared<ast::BinaryOperation>(nullptr, $1, ast::Lexer::ParsedBinaryOperation::Modulo, $3); }
  | "++" value			{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::PrefixIncrement, $2); }
  | "--" value			{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::PrefixDecrement, $2); }
  | "+" value			%prec UNOP_UNARY_PLUS			{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::UnaryPlus, $2); }
  | "-" value			%prec UNOP_UNARY_MINUS			{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::UnaryMinus, $2); }
  | "!" value			{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::LogicalNot, $2); }
  | "~" value			{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::BitwiseNot, $2); }
  | "(bool)" value		{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::TypecastBool, $2); }
  | "(int)" value		{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::TypecastInt, $2); }
  | "(real)" value		{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::TypecastReal, $2); }
  | "(string)" value	{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::TypecastString, $2); }
  | value "++"			%prec UNOP_POSTFIX_INCREMENT	{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::PostfixIncrement, $1); }
  | value "--"			%prec UNOP_POSTFIX_DECREMENT	{ $$ = std::make_shared<ast::UnaryOperation>(nullptr, ast::Lexer::ParsedUnaryOperation::PostfixDecrement, $1); }
%%



#include "Main.hpp"

void yy::ScratchCodeParser::error(const yy::location& loc, const std::string& message)
{
	driver.handleError(loc, message);
}

